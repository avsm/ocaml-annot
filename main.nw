

\section{Main Module}

Everything starts at the main module: it parses the command line and
calls functions that do the actual work. Catching and reporting errors
to the user is also the main modules' responsibility. The Main module
has an interface but since [[main]] is called automatically it is not of
much use.

<<main.mli>>=
val main: unit -> unit (* executed automatically *)
@

\subsection{Implementation}

<<main.ml>>=
exception Usage of string

let error   fmt = Printf.kprintf (fun msg -> raise (Usage msg)) fmt
let printf      = Printf.printf
let sprintf     = Printf.sprintf
let version     = "$Id$"
let (@@) f x    = f x

let this        = Sys.argv.(0)

let synopsis =
    [ "usage: "^this^" [options]" 
    ; ""
    ; "-h, -help                        offer minimal help on stdout"
    ; "-version                         identify version of "^this^" on stdout"
    ; "-type line column [file.annot]   lookup type annotation for position"
    ; "-dump [file.annot]               dump annotations to stdout"
    ; ""
    ; "Author: Christian Lindig http://www.st.cs.uni-sb.de/~lindig/"
    ]
    
let usage msg = List.iter prerr_endline (msg :: synopsis)
let help ()   = List.iter print_endline synopsis

let version () =
    List.iter print_endline
    [ this^" version $Id$"
    ; "(c) 2006 Christian Lindig <lindig@cs.uni-sb.de>"
    ]
@

<<main.ml>>=
module A = Annot.ByPosition
@

Operation [[with_file]] opens a file for reading and passes the file
handle to an action. It takes care of closing the file, even in the case
of errors.

<<main.ml>>=
let with_file file action =
    let ic = open_in file in
    try 
        let result = action ic in
        close_in ic; result
    with
        x -> close_in ic; raise x

let annotations_from channel = 
    let lexbuf = Lexing.from_channel channel in
    try
        Parser.annotations Scanner.token lexbuf
    with
        Parsing.Parse_error -> 
            error "parse error at offset %d" (Lexing.lexeme_start lexbuf)

let int x   = try int_of_string x with Failure _ -> error "not an int: %s" x
@

Read type annotation from channel [[ic]] and report the annotation for a
given [[line]] and [[column]] to stdout. We first build an interval map
and then search it for the given position.

<<main.ml>>=
let typeannot line column ic = 
    let line, column = int line, int column in
    let annots = annotations_from ic in
        match A.find annots (line, column) with
        | Some s -> print_endline s
        | None   -> print_endline "no type found"
@    
 
Read annotations from channel [[ic]], build an interval map from it and
dump it to stdout (for debugging).

<<main.ml>>= 
let dump ic = A.dump (fun x -> x) @@ annotations_from ic
        
 
let main () =
    let argv    = Array.to_list Sys.argv in
    let args    = List.tl argv in
    let rec parse = function
        | "-h"               :: [] -> help ()
        | "-help"            :: [] -> help ()
        | "-version"         :: [] -> version ()
        | ["-type"; line;col;file] -> with_file file @@ typeannot line col 
        | ["-type"; line;col]      -> typeannot line col stdin
        | ["-dump"; file]          -> with_file file @@ dump
        | ["-dump"]                -> dump stdin
        | x :: _ -> usage @@ sprintf "error: illegal command line argument %s" x
        | []     -> usage "error: expected an option"
    in              
        try parse args with 
        |   Usage     msg -> (usage msg; exit 1)
        |   Sys_error msg -> (print_endline msg; exit 1)

let () = if !Sys.interactive then () else main ()
@



